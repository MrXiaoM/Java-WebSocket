plugins {
    id 'java'

    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'top.mrxiaom'
version = '1.5.8'
sourceCompatibility = 1.8
targetCompatibility = 1.8
java {
    withJavadocJar()
    withSourcesJar()
}
compileJava {
    options.compilerArgs += ['-encoding', 'UTF-8']
}

javadoc {
    options.encoding = 'UTF-8'
    options.addBooleanOption("keywords", true)
    options.addBooleanOption("Xdoclint:none", true)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'Java-WebSocket'
            version = project.version

            from components.java
            pom {
                name = artifactId
                description = "A barebones WebSocket client and server implementation written in 100% Java."
                url = "https://github.com/MrXiaoM/Java-WebSocket"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/MrXiaoM/Java-WebSocket/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        name = "MrXiaoM"
                        email = "mrxiaom@qq.com"
                    }
                }
                scm {
                    url = "https://github.com/MrXiaoM/Java-WebSocket"
                    connection = "scm:git:https://github.com/MrXiaoM/Java-WebSocket.git"
                    developerConnection = "scm:git:https://github.com/MrXiaoM/Java-WebSocket.git"
                }
            }
        }
    }
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.13'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.13'
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}
